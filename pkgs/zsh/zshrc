# vim: set ft=zsh

# History
setopt histignorealldups
HISTSIZE=1000000
SAVEHIST=1000000
HISTFILE="$HOME/.cache/zsh/zsh_history"
mkdir -p "$(dirname "$HISTFILE")"

# Utils
autoload -Uz colors && colors
autoload -U zmv
alias zmv="noglob zmv -v"
alias zcp="noglob zmv -vC"
alias zln="noglob zmv -vLs"

setopt extendedglob interactivecomments autocd
setopt auto_continue noflowcontrol
setopt rmstarsilent

WORDCHARS='*?_-.[]~=&;!#$%^(){}<>' # exclude '/' for backward-kill-word
REPORTTIME=3 # report elapsed time (> 3 sec.)

nowrap() { echoti rmam; $@; a=$?; echoti smam; return $a }

mkcd() {
	mkdir -p $1
	cd $1
}

alias locate="locate --basename"
alias sudo="sudo env HOME=$HOME "
alias su="sudo $SHELL"
alias pdflatex="true|pdflatex"
alias lualatex="true|lualatex"
alias diff="git diff --no-index"
alias ls="ls --color=auto -F"
alias grep="grep --color=auto"
alias chafa="chafa --symbols=vhalf"

zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

alias o=xdg-open
alias \$= # Allows copying scripts with $ prompts

for c in cp rm mv chmod chown rename; do
	alias $c="$c -v"
done

reentry_hook() {
	if [[ $(stat -c "%h" .) -eq 0 ]]; then
		if [[ -d $PWD ]]; then
			echo "PWD recreated"
			builtin cd .
		else
			echo "PWD nonexistent!"
		fi
	fi
}
autoload -Uz add-zsh-hook && add-zsh-hook preexec reentry_hook

# Completion
autoload -Uz compinit && compinit -u
setopt complete_in_word
setopt always_to_end
zmodload -i zsh/complist

alias zstyle="noglob zstyle"
zstyle ':completion:*' format "%B%F{yellow}%U%d%f%b%u"
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}'
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle ':completion:*' menu select
zstyle ':completion:*' list-separator '|'
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' completer _complete _approximate _ignored
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:complete:*' use-cache 1
zstyle ':completion:*:complete:*' cache-path ~/.zsh_cache
zstyle -e ':completion:*:approximate:*' max-errors 'reply=( $(( ($#PREFIX+$#SUFFIX)/3 )) )'

zstyle ':completion:*:original' list-colors "=*=$color[red];$color[bold]"
zstyle ':completion:*:processes' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'
zstyle ':completion:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'

zstyle ':completion:*:*:*:users' ignored-patterns \
	adm amanda apache avahi beaglidx bin cacti canna clamav daemon dbus \
	distcache dovecot fax ftp games gdm gkrellmd gopher hacluster haldaemon \
	halt hsqldb ident junkbust ldap lp mail mailman mailnull mldonkey mysql \
	nagios named netdump news nfsnobody nobody nscd ntp nut nx openvpn \
	operator pcap postfix postgres privoxy pulse pvm quagga radvd rpc rpcuser \
	rpm shutdown squid sshd sync uucp vcsa xfs systemd-*

autoload -U regexp-replace
setopt re_match_pcre

# history-search-multi-word
zstyle ":history-search-multi-word" page-size 25
zstyle ":history-search-multi-word" highlight-color "standout"

# fast-syntax-highlighting
HIGHLIGHT=(
	"path"     "fg=cyan"
	"variable" "fg=green,bold"
)
typeset -gA FAST_HIGHLIGHT_STYLES
FAST_HIGHLIGHT_STYLES+=(${(kv)HIGHLIGHT})
FAST_HIGHLIGHT[chroma-man]=
